open import Pervasives
import List_extra
import String
import String_extra
open import Lib
open import Ast

(* The type that a constructor builds is either a named datatype or an exception.
 * For exceptions, we also keep the module that the exception was declared in. *)
type tid_or_exn = 
  | TypeId of id typeN
  | TypeExn of id conN

val type_defs_to_new_tdecs : maybe modN -> type_def -> set tid_or_exn
let type_defs_to_new_tdecs mn tdefs =
  Set.fromList (List.map (fun (tvs,tn,ctors) -> TypeId (mk_id mn tn)) tdefs)

(* Maps each constructor to its arity and which type it is from *)
type flat_envC = env conN (nat * tid_or_exn)
type envC = env modN flat_envC * flat_envC

val merge_envC : envC -> envC -> envC
let merge_envC (mcenv1,cenv1) (mcenv2,cenv2) = 
  (merge mcenv1 mcenv2, merge cenv1 cenv2)

(* Value forms *)
type v =
  | Litv of lit
  (* Constructor application. *)
  | Conv of maybe (conN * tid_or_exn) * list v 
  (* Function closures
     The environment is used for the free variables in the function *)
  | Closure of (env modN (env varN v) * envC * env varN v) * varN * exp
  (* Function closure for recursive functions
   * See Closure and Letrec above
   * The last variable name indicates which function from the mutually
   * recursive bundle this closure value represents *)
  | Recclosure of (env modN (env varN v) * envC * env varN v) * list (varN * varN * exp) * varN
  | Loc of nat
  | Vectorv of list v

type envE = env varN v

(* The bindings of a module *)
type envM = env modN envE

type all_env = envM * envC * envE

let all_env_to_menv (menv,cenv,env) = menv
let all_env_to_cenv (menv,cenv,env) = cenv
let all_env_to_env (menv,cenv,env) = env

(* The result of evaluation *)
type error_result 'a =
  | Rtype_error
  | Rraise of 'a (* Should only be a value of type exn *)
  | Rtimeout_error

type result 'a 'b =
  | Rval of 'a
  | Rerr of error_result 'b

(* Stores *)
type store_v 'a = 
  (* A ref cell *) 
    Refv of 'a 
  (* A byte array *)
  | W8array of list word8 
  (* An array of values *) 
  | Varray of list 'a

val store_v_same_type : forall 'a. store_v 'a -> store_v 'a -> bool
let store_v_same_type v1 v2 =
  match (v1,v2) with
  | (Refv _, Refv _) -> true
  | (W8array _,W8array _) -> true
  | (Varray _,Varray _) -> true
  | _ -> false
  end

(* The nth item in the list is the value at location n *)
type store 'a = list (store_v 'a)

val empty_store : forall 'a. store 'a
let empty_store = []

val store_lookup : forall 'a. nat -> store 'a -> maybe (store_v 'a)
let store_lookup l st =
  if l < List.length st then
    Just (List_extra.nth st l)
  else
    Nothing

val store_alloc : forall 'a. store_v 'a -> store 'a -> store 'a * nat
let store_alloc v st =
  ((st ++ [v]), List.length st)

val store_assign : forall 'a. nat -> store_v 'a -> store 'a -> maybe (store 'a)
let store_assign n v st =
  if n < List.length st &&
     store_v_same_type (List_extra.nth st n) v
  then
    Just (List.update st n v)
  else
    Nothing

val lookup_var_id : id varN -> all_env -> maybe v
let lookup_var_id id (menv,cenv,env) =
  match id with
    | Short x -> lookup x env
    | Long x y ->
        match lookup x menv with
          | Nothing -> Nothing
          | Just env -> lookup y env
        end
  end

val lookup_con_id : forall 'a. id conN -> env modN (env conN 'a) * env conN 'a -> maybe 'a
let lookup_con_id id (mcenv,cenv) =
  match id with
    | Short x -> lookup x cenv
    | Long x y ->
        match lookup x mcenv with
          | Nothing -> Nothing
          | Just cenv -> lookup y cenv
        end
  end

(* Other primitives *)
(* Check that a constructor is properly applied *)
val do_con_check : envC -> maybe (id conN) -> nat -> bool
let do_con_check cenv n_opt l =
  match n_opt with
    | Nothing -> true
    | Just n ->
        match lookup_con_id n cenv with
          | Nothing -> false
          | Just (l',ns) -> l = l'
        end
  end

val build_conv : envC -> maybe (id conN) -> list v -> maybe v
let build_conv envC cn vs =
  match cn with
    | Nothing -> 
        Just (Conv Nothing vs)
    | Just id -> 
        match lookup_con_id id envC with
          | Nothing -> Nothing
          | Just (len,t) -> Just (Conv (Just (id_to_n id, t)) vs)
        end
  end

val lit_same_type : lit -> lit -> bool
let lit_same_type l1 l2 =
  match (l1,l2) with
    | (IntLit _, IntLit _) -> true
    | (StrLit _, StrLit _) -> true
    | (Bool _, Bool _) -> true
    | (Unit, Unit) -> true
    | (Word8 _, Word8 _) -> true
    | _ -> false
  end

type match_result 'a =
  | No_match
  | Match_type_error
  | Match of 'a

val same_tid : tid_or_exn -> tid_or_exn -> bool
let rec same_tid (TypeId tn1) (TypeId tn2) = tn1 = tn2
and same_tid (TypeExn _) (TypeExn _) = true
and same_tid _ _ = false

val same_ctor : conN * tid_or_exn -> conN * tid_or_exn -> bool
let rec same_ctor (cn1, TypeExn mn1) (cn2, TypeExn mn2) = cn1 = cn2 && mn1 = mn2
and same_ctor (cn1, _) (cn2, _) = cn1 = cn2

(* A big-step pattern matcher.  If the value matches the pattern, return an
 * environment with the pattern variables bound to the corresponding sub-terms
 * of the value; this environment extends the environment given as an argument.
 * No_match is returned when there is no match, but any constructors
 * encountered in determining the match failure are applied to the correct
 * number of arguments, and constructors in corresponding positions in the
 * pattern and value come from the same type.  Match_type_error is returned
 * when one of these conditions is violated *)
val pmatch : envC -> store v -> pat -> v -> envE -> match_result envE
let rec
pmatch envC s (Pvar x) v' env = Match (bind x v' env)
and
pmatch envC s (Plit l) (Litv l') env =
  if l = l' then
    Match env
  else if lit_same_type l l' then
    No_match
  else
    Match_type_error
and
pmatch envC s (Pcon (Just n) ps) (Conv (Just (n', t')) vs) env =
  match lookup_con_id n envC with
    | Just (l, t)->
        if same_tid t t' && List.length ps = l then
          if same_ctor (id_to_n n, t) (n',t') then
            pmatch_list envC s ps vs env
          else
            No_match
        else
          Match_type_error
    | _ -> Match_type_error
  end
and
pmatch envC s (Pcon Nothing ps) (Conv Nothing vs) env =
  if List.length ps = List.length vs then
    pmatch_list envC s ps vs env
  else
    Match_type_error
and
pmatch envC s (Pref p) (Loc lnum) env =
  match store_lookup lnum s with
    | Just (Refv v) -> pmatch envC s p v env
    | Just _ -> Match_type_error
    | Nothing -> Match_type_error
  end
and
pmatch envC _ _ _ env = Match_type_error
and
pmatch_list envC s [] [] env = Match env
and
pmatch_list envC s (p::ps) (v::vs) env =
  match pmatch envC s p v env with
    | No_match -> No_match
    | Match_type_error -> Match_type_error
    | Match env' -> pmatch_list envC s ps vs env'
  end
and
pmatch_list envC s _ _ env = Match_type_error

(* Bind each function of a mutually recursive set of functions to its closure *)
val build_rec_env : list (varN * varN * exp) -> all_env -> envE -> envE
let build_rec_env funs cl_env add_to_env =
  foldr 
    (fun (f,x,e) env' -> bind f (Recclosure cl_env funs f) env') 
    add_to_env 
    funs

(* Lookup in the list of mutually recursive functions *)
val find_recfun : forall 'a 'b. varN -> list (varN * 'a * 'b) -> maybe ('a * 'b)
let rec find_recfun n funs =
  match funs with
    | [] -> Nothing
    | (f,x,e) :: funs ->
        if f = n then
          Just (x,e)
        else
          find_recfun n funs
  end

declare termination_argument find_recfun = automatic

(* Check whether a value contains a closure, but don't indirect through the store *)
val contains_closure : v -> bool
let rec
contains_closure (Litv l) = false
and
contains_closure (Conv cn vs) = List.any contains_closure vs
and
contains_closure (Closure env n e) = true
and
contains_closure (Recclosure env funs n) = true
and
contains_closure (Loc n) = false
and
contains_closure (Vectorv vs) = List.any contains_closure vs

type eq_result = 
  | Eq_val of bool
  | Eq_closure
  | Eq_type_error

val do_eq : v -> v -> eq_result
let rec
do_eq (Litv l1) (Litv l2) =
  if lit_same_type l1 l2 then Eq_val (l1 = l2)
  else Eq_type_error
and
do_eq (Loc l1) (Loc l2) = Eq_val (l1 = l2)
and
do_eq (Conv cn1 vs1) (Conv cn2 vs2) =
  if cn1 = cn2 && (List.length vs1 = List.length vs2) then
    do_eq_list vs1 vs2
  else
    Eq_val false
and
do_eq (Vectorv vs1) (Vectorv vs2) =
  if List.length vs1 = List.length vs2 then
    do_eq_list vs1 vs2
  else
    Eq_val false
and
do_eq (Closure _ _ _) (Closure _ _ _) = Eq_closure
and
do_eq (Closure _ _ _) (Recclosure _ _ _) = Eq_closure
and
do_eq (Recclosure _ _ _) (Closure _ _ _) = Eq_closure
and
do_eq (Recclosure _ _ _) (Recclosure _ _ _) = Eq_closure
and
do_eq _ _ = Eq_type_error
and
do_eq_list [] [] = Eq_val true
and
do_eq_list (v1::vs1) (v2::vs2) = 
  match do_eq v1 v2 with
    | Eq_closure -> Eq_closure
    | Eq_type_error -> Eq_type_error
    | Eq_val r -> 
        if not r then
          Eq_val false
        else
          do_eq_list vs1 vs2
  end
and
do_eq_list _ _ = Eq_val false

val prim_exn : conN -> v
let prim_exn cn = Conv (Just (cn, TypeExn (Short cn))) []

(* Do an application *)
val do_opapp : list v -> maybe (all_env * exp)
let do_opapp vs =
  match vs with
  | [Closure (menv, cenv, env) n e; v] ->
      Just ((menv, cenv, bind n v env), e)
  | [Recclosure (menv, cenv, env) funs n; v] ->
      if allDistinct (List.map (fun (f,x,e) -> f) funs) then
        match find_recfun n funs with
          | Just (n,e) -> Just ((menv, cenv, bind n v (build_rec_env funs (menv, cenv, env) env)), e)
          | Nothing -> Nothing
        end
      else
        Nothing
  | _ -> Nothing
  end

(* If a value represents a list, get that list. Otherwise return Nothing *)
val v_to_list : v -> maybe (list v)
let rec v_to_list (Conv (Just (cn, TypeId (Short tn))) []) = 
  if cn = "nil" && tn = "list" then
    Just []
  else
    Nothing
and v_to_list (Conv (Just (cn,TypeId (Short tn))) [v1;v2]) =
  if cn = "::"  && tn = "list" then
    match v_to_list v2 with
      | Just vs -> Just (v1::vs)
      | Nothing -> Nothing
    end
  else
    Nothing
and v_to_list _ = Nothing

val opn_lookup : opn -> integer -> integer -> integer
let opn_lookup n : integer -> integer -> integer = match n with
  | Plus -> (+)
  | Minus -> (-)
  | Times -> ( * )
  | Divide -> (/)
  | Modulo -> (mod)
end

val opb_lookup : opb -> integer -> integer -> bool
let opb_lookup n : integer -> integer -> bool = match n with
  | Lt -> (<)
  | Gt -> (>)
  | Leq -> (<=)
  | Geq -> (>=)
end

val do_app : store v -> op -> list v -> maybe (store v * result v v)
let do_app s op vs =
  match (op, vs) with
    | (Opn op, [Litv (IntLit n1); Litv (IntLit n2)]) ->
        if (op = Divide || (op = Modulo)) && (n2 = 0) then
          Just (s, Rerr (Rraise (prim_exn "Div")))
        else
          Just (s, Rval (Litv (IntLit (opn_lookup op n1 n2))))
    | (Opb op, [Litv (IntLit n1); Litv (IntLit n2)]) ->
        Just (s, Rval (Litv (Bool (opb_lookup op n1 n2))))
    | (Equality, [v1; v2]) ->
        match do_eq v1 v2 with
          | Eq_type_error -> Nothing
          | Eq_closure -> Just (s, Rerr (Rraise (prim_exn "Eq")))
          | Eq_val b -> Just (s, Rval (Litv (Bool b)))
        end
    | (Opassign, [Loc lnum; v]) ->
        match store_assign lnum (Refv v) s with
          | Just st -> Just (st, Rval (Litv Unit))
          | Nothing -> Nothing
        end
    | (Opref, [v]) ->
        let (s',n) = store_alloc (Refv v) s in
          Just (s', Rval (Loc n))
    | (Opderef, [Loc n]) ->
        match store_lookup n s with
          | Just (Refv v) -> Just (s,Rval v)
          | _ -> Nothing
        end
    | (Aw8alloc, [Litv (IntLit n); Litv (Word8 w)]) ->
        if n < 0 then
          Just (s, Rerr (Rraise (prim_exn "Subscript")))
        else
          let (s',lnum) =
            store_alloc (W8array (List.replicate (natFromInteger n) w)) s
          in 
            Just (s', Rval (Loc lnum))
    | (Aw8sub, [Loc lnum; Litv (IntLit i)]) ->
        match store_lookup lnum s with
          | Just (W8array ws) ->
              if i < 0 then
                Just (s, Rerr (Rraise (prim_exn "Subscript")))
              else
                let n = natFromInteger i in
                  if n >= List.length ws then
                    Just (s, Rerr (Rraise (prim_exn "Subscript")))
                  else 
                    Just (s, Rval (Litv (Word8 (List_extra.nth ws n))))
          | _ -> Nothing
        end
    | (Aw8length, [Loc n]) ->
        match store_lookup n s with
          | Just (W8array ws) ->
              Just (s,Rval (Litv(IntLit(integerFromNat(List.length ws)))))
          | _ -> Nothing
         end
    | (Aw8update, [Loc lnum; Litv(IntLit i); Litv(Word8 w)]) ->
        match store_lookup lnum s with
        | Just (W8array ws) ->
            if i < 0 then
              Just (s, Rerr (Rraise (prim_exn "Subscript")))
            else 
              let n = natFromInteger i in
                if n >= List.length ws then
                  Just (s, Rerr (Rraise (prim_exn "Subscript")))
                else
                  match store_assign lnum (W8array (List.update ws n w)) s with
                    | Nothing -> Nothing
                    | Just s' -> Just (s', Rval (Litv Unit))
                  end
        | _ -> Nothing
      end
    | (VfromList, [v]) ->
          match v_to_list v with
            | Just vs ->
                Just (s, Rval (Vectorv vs))
            | Nothing -> Nothing
          end
    | (Vsub, [Vectorv vs; Litv (IntLit i)]) ->
        if i < 0 then
          Just (s, Rerr (Rraise (prim_exn "Subscript")))
        else
          let n = natFromInteger i in
            if n >= List.length vs then
              Just (s, Rerr (Rraise (prim_exn "Subscript")))
            else 
              Just (s, Rval (List_extra.nth vs n))
    | (Vlength, [Vectorv vs]) ->
        Just (s, Rval (Litv (IntLit (integerFromNat (List.length vs)))))
    | (Aalloc, [Litv (IntLit n); v]) ->
        if n < 0 then
          Just (s, Rerr (Rraise (prim_exn "Subscript")))
        else
          let (s',lnum) =
            store_alloc (Varray (List.replicate (natFromInteger n) v)) s
          in 
            Just (s', Rval (Loc lnum))
    | (Asub, [Loc lnum; Litv (IntLit i)]) ->
        match store_lookup lnum s with
          | Just (Varray vs) ->
              if i < 0 then
                Just (s, Rerr (Rraise (prim_exn "Subscript")))
              else
                let n = natFromInteger i in
                  if n >= List.length vs then
                    Just (s, Rerr (Rraise (prim_exn "Subscript")))
                  else 
                    Just (s, Rval (List_extra.nth vs n))
          | _ -> Nothing
        end
    | (Alength, [Loc n]) ->
        match store_lookup n s with
          | Just (Varray ws) ->
              Just (s,Rval (Litv(IntLit(integerFromNat(List.length ws)))))
          | _ -> Nothing
         end
    | (Aupdate, [Loc lnum; Litv (IntLit i); v]) ->
        match store_lookup lnum s with
        | Just (Varray vs) ->
            if i < 0 then
              Just (s, Rerr (Rraise (prim_exn "Subscript")))
            else 
              let n = natFromInteger i in
                if n >= List.length vs then
                  Just (s, Rerr (Rraise (prim_exn "Subscript")))
                else
                  match store_assign lnum (Varray (List.update vs n v)) s with
                    | Nothing -> Nothing
                    | Just s' -> Just (s', Rval (Litv Unit))
                  end
        | _ -> Nothing
      end
    | _ -> Nothing
  end

(* Do a logical operation *)
val do_log : lop -> v -> exp -> maybe exp
let do_log l v e =
  match (l, v) with
    | (And, Litv (Bool true)) -> Just e
    | (Or, Litv (Bool false)) -> Just e
    | (_, Litv (Bool b)) -> Just (Lit (Bool b))
    | _ -> Nothing
  end

(* Do an if-then-else *)
val do_if : v -> exp -> exp -> maybe exp
let do_if v e1 e2 =
  if v = Litv (Bool true) then
    Just e1
  else if v = Litv (Bool false) then
    Just e2
  else
    Nothing

(* Semantic helpers for definitions *)

(* Build a constructor environment for the type definition tds *)
val build_tdefs : maybe modN -> list (list tvarN * typeN * list (conN * list t)) -> flat_envC
let build_tdefs mn tds =
  List.reverse
    (concat
      (List.map
        (fun (tvs, tn, condefs) ->
           List.map
             (fun (conN, ts) ->
                (conN, (List.length ts, TypeId (mk_id mn tn))))
             condefs)
        tds))

(* Checks that no constructor is defined twice in a type *)
val check_dup_ctors : list (list tvarN * typeN * list (conN * list t)) -> bool
let check_dup_ctors tds =
  List.allDistinct [ n | forall ((tvs, tn, condefs) MEM tds) ((n, ts) MEM condefs) | true ]

val combine_dec_result : forall 'a 'b 'c. env 'a 'b -> result (env 'a 'b) 'c -> result (env 'a 'b) 'c
let combine_dec_result env r =
  match r with
    | Rerr e -> Rerr e
    | Rval env' -> Rval (merge env' env)
  end

val combine_mod_result : forall 'a 'b 'c 'd 'e. env 'a 'b -> env 'c 'd -> result (env 'a 'b * env 'c 'd) 'e -> result (env 'a 'b * env 'c 'd) 'e
let combine_mod_result menv env r =
  match r with
    | Rerr e -> Rerr e
    | Rval (menv',env') -> Rval (merge menv' menv, merge env' env)
  end

let remove_count ((count,store),tdecls,mods) = (store,tdecls,mods)

(* conversions to strings *)

let rec
id_to_string (Short s) = s
and
id_to_string (Long x y) = x^"."^y

let tc_to_string tc =
  match tc with
    TC_name id -> id_to_string id
  | TC_int -> "<int>"
  | TC_string -> "<string>"
  | TC_bool -> "<bool>"
  | TC_unit -> "<unit>"
  | TC_ref -> "<ref>"
  | TC_word8 -> "<word8>"
  | TC_word8array -> "<word8array>"
  | TC_exn -> "<exn>"
  | TC_vector -> "<vector>"
  | TC_array -> "<array>"
  end

val int_to_string : integer -> string
let int_to_string z =
  if z < 0 then "~"^(String_extra.show (naturalFromInteger (~ z)))
  else String_extra.show (naturalFromInteger z)


let rec
string_escape [] = ""
and
string_escape (c::cs) =
  (if c = #'\n' then "\\n"
   else if c = #'\t' then "\\t"
   else if c = #'\\' then "\\\\"
   else String.toString [c])
  ^(string_escape cs)

let string_to_string s =
  "\""^(string_escape (String.toCharList s))^"\""
